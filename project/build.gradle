plugins {
    id 'java'
    id 'application'
  //  id 'pmd'
    id 'edu.odu.cs.report_accumulator' version '1.4'
    id 'jacoco'
}

java {
    sourceCompatibility = JavaVersion.toVersion(11)
    targetCompatibility = JavaVersion.toVersion(11)
}

repositories {
    mavenCentral()
}

application {
    mainClass = 'odu.edu.cs.cs350.pne.Driver'
}

dependencies {
    implementation 'com.opencsv:opencsv:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    implementation 'org.apache.poi:poi-ooxml-schemas:4.1.2'
    implementation 'org.apache.poi:poi-ooxml:4.1.2'
}

test {
    ignoreFailures = true
    useJUnitPlatform()
}

jar {
    duplicatesStrategy = 'exclude'
    manifest {
        attributes 'Implementation-Title' : 'Enrollment Projection',
            'Implementaion-Version' : '1.0',
            'Main-Class' : 'edu.odu.cs.cs350.pne.Driver'  
    }
    from { configurations.runtimeClasspath.collect {
        it.isDirectory() ? it : zipTree(it) }
    }
}

task copyDocs(type: Copy, dependsOn: javadoc){
    from(file('build/docs'))
    into(file('build/reports'))
}

check.dependsOn(copyDocs)

task reports(type: Copy, dependsOn: 'build'){
    from 'src/main/html'
    into 'build/reports'
}

reportStats {
    reportsURL = 'https://jmill035.github.io/Enrollment-Projection/'
    htmlSourceDir = file('src/main/html') 
    reportsDir = file('build/reports') 
}

def pagesDir = file("${project.rootDir}/../website-temp").absolutePath

task clearPages(type: Delete) {
    delete pagesDir
}

task workTree(dependsOn: ['clearPages']) { 
    doLast {
        exec {
            workingDir = '.'
            commandLine = ['git', 'worktree', 'add', '-f', pagesDir, 'gh-pages']
        }
        exec {
            workingDir = pagesDir
            commandLine = ['git', 'checkout', 'gh-pages']
        }
        exec {
            workingDir = pagesDir
            commandLine = ['git', 'pull']
        }
    }
}

task copyReports (type: Copy, dependsOn: ['reports', 'workTree']) {
    from 'build/reports'
    into pagesDir
}


task copyWebsite (dependsOn: ['copyReports']) {
}

task commitWebsite (dependsOn: 'copyWebsite') {
    doLast {
        Date now = new Date()

        exec {
            workingDir = pagesDir
            commandLine = ['git', 'add', '.', pagesDir]
            ignoreExitValue = true
        }
        exec {
            workingDir = pagesDir
            commandLine = ['git', 'commit', '-m', 'Updating webpages at ' + now.toString()]
            ignoreExitValue = true
        }
    }
}

task pushWebsite (type: Exec, dependsOn: 'commitWebsite') {
    workingDir = pagesDir
    commandLine = ['git', 'push']
    ignoreExitValue = true
}

task deployReports (type: Delete, dependsOn: 'pushWebsite') {
    group = "Reporting"
    description  'Commit and push website changes to GitHub'
    delete pagesDir
}
